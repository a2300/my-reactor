plugins {
    id 'java'
    id 'eclipse'
    id 'org.springframework.boot' version '3.2.5'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'org.openapi.generator' version '7.7.0'

    id "jacoco"
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

repositories {
    mavenCentral()
}

dependencies {

    compileOnly "org.projectlombok:lombok:1.18.28"
    annotationProcessor "org.projectlombok:lombok:1.18.28"

    implementation 'org.springframework.boot:spring-boot-starter-web'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.named('compileJava') {
    dependsOn(tasks.openApiGenerate)
}

sourceSets.main.java.srcDir "${buildDir}/generate-resources/main/src/main/java"

openApiGenerate {
    generatorName.set('java')
    configOptions.set([
            library: 'restclient',
            openApiNullable: 'false'
    ])
    inputSpec.set("$projectDir/petclinic-spec.yml")
    ignoreFileOverride.set("$projectDir/.openapi-generator-java-sources.ignore")
    invokerPackage.set('com.myapp')
    modelPackage.set('com.myapp.model')
    apiPackage.set('com.myapp.api')
}

def jacocoExclusions = [
        'com/example/**/QuotesApplication.class',
        'com/example/**/GlobalExceptionHandler.class',
        'com/example/**/QuoteIndexOutOfBoundsException.class'
]

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        xml.destination file("$buildDir/reports/jacoco/test/jacocoTestReport.xml")
        csv.required = false
        html.required = true
    }
}
jacocoTestCoverageVerification {
    dependsOn test
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: jacocoExclusions)
        }))
    }
    violationRules {
        failOnViolation = true
        rule {
            limit {
                minimum = 0.8
            }
            excludes = [
                   'com.example.QuotesApplication',
                   'com.example.GlobalExceptionHandler',
                   'com.example.QuoteIndexOutOfBoundsException'
            ]
        }
    }
}

check {
    dependsOn jacocoTestCoverageVerification
}

test {
    finalizedBy jacocoTestReport
}
